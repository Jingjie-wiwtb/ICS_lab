# name：贺劲洁
# ID：18307130370
# Execution begins at address 0
   .pos 0
   irmovq stack, %rsp
   call main
   halt
   
# Sample linked list
.align 8
list: 
   ele1:
      .quad 0x00a
	  .quad ele2
   ele2:
      .quad 0x0b0
	  .quad ele3
   ele3:
      .quad 0xc00
	  .quad 0		 
		
main:
   irmovq list, %rdi
   call list_sum
   ret
     
#long sum_list(list_ptr ls)
# ls in %rdi, return val in %rax
list_sum:
   irmovq $0, %rax        #long val = 0;
   andq   %rdi,%rdi         #set CC (ls?=0)
   je     end
   
loop:
   mrmovq (%rdi),%rdx     #ls->val
   addq   %rdx, %rax      #val += ls->val
   mrmovq 8(%rdi),%rdi    #ls = ls->next
   andq   %rdi,%rdi         #set CC (ls?=0)
   jne    loop
   
end:
   ret
 
 
#Stack starts here and grows to lower addresses
   .pos 0x200
stack:
   
   
   
   
   
   
   
   
