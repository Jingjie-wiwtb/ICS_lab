# name：贺劲洁
# ID：18307130370

# Execution begins at address 0
   .pos 0
   irmovq stack, %rsp
   call main
   halt
 
.align 8

#Source block
src:
   .quad 0x00a
   .quad 0x0b0
   .quad 0xc00
	  
#Destination block
dest:
   .quad 0x111
   .quad 0x222
   .quad 0x333
		
main:
   irmovq src, %rdi
   irmovq dest, %rsi
   irmovq $3, %rdx
   call copy_block
   ret
      
#long copy_block(long *src, long *dest, long len)
# src in %rdi, dest in %rsi, len in %rdx, return val in %rax

copy_block:
   irmovq $8, %r12
   irmovq $1, %r13
   irmovq $0, %rax        #long result = 0;
   andq   %rdx,%rdx       #set CC (len?=0)
   jle     end
   
loop: 
   mrmovq (%rdi),%rbx     #long val = *src;
   addq   %r12, %rdi      #src++;
   rmmovq %rbx,(%rsi)     #*dest = val
   addq   %r12, %rsi      #dest++
   xorq   %rbx, %rax      #result ^= val
   subq   %r13, %rdx      #len--   setCC
   jne    loop
   
end:
   ret
 
 
#Stack starts here and grows to lower addresses
   .pos 0x200
stack:
   
   
   
   
   
   
